/JOB
//NAME SRCH-2D-R1
///FOLDERNAME INNE
//POS
///NPOS 0,0,0,0,0,0
//INST
///DATE 2023/10/04 15:12
///ATTR SC,RW
///GROUP1 RB1,BS1
///LVARS 3,0,0,0,0,6,6,0
NOP
'---SEARCHING 2 DIMENSIONS------
'---SELECT GLOBAL PVAR TO SHIFT-
SET LB001 10
'---SELECT DIRECT INPUT NR-------
SET LB002 1
'--------------------------------
GETS LPX001 $PX014
CNVRT LPX001 LPX001 RF
GETS LPX002 $PX015
CNVRT LPX002 LPX002 RF
GETS LPX003 $PX016
CNVRT LPX003 LPX003 RF
MOVL LP001 V=33.3 PL=0
'---SEARCHING 1------------------
WAIT SOUT#(394)=OFF
MOVL LP002 V=15.0 SRCH RIN#(LB002)=ON T=0.20 DIS=10.0
GETS LB000 $B002
JUMP *ERR IF LB000=0
'---CALCULATION 1----------------
GETS LPX004 $PX002
CNVRT LPX004 LPX004 RF
SUB LP004 LP002
SFTON LP004 RF
MOVL LP001 V=25.0
'---SEARCHING 2------------------
WAIT SOUT#(394)=OFF
MOVL LP003 V=15.0 SRCH RIN#(LB002)=ON T=0.20 DIS=10.0
GETS LB000 $B002
JUMP *ERR IF LB000=0
'---CALCULATION 2----------------
GETS LPX005 $PX002
CNVRT LPX005 LPX005 RF
SUB LP005 LP003
SET P[LB001] LP005
MOVL LP001 V=25.0
SFTOF
SETE P[LB001] (4) 0
SETE P[LB001] (5) 0
SETE P[LB001] (6) 0
RET
'WRITE SFTON P0x BEFORE
'POINT TO BE SHIFTED
*ERR
'NO CONTACT WHEN SEARCHING
PAUSE
MOVL LP001 V=25.0
RET
'********************************
'*    SEARCH JOB 2 DIMENSION    *
'*      __ __ _____ _____       *
'*     |  |  |   | | __  |      *
'*     |_   _| | | |    -|      *
'*       |_| |_|___|__|__|      *
'*                              *
'*         2018-01-09           *
'********************************
END
