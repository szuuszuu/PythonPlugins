/JOB
//NAME LTCLIB
//POS
///NPOS 0,0,0,0,0,0
//INST
///DATE 2012/06/19 11:24
///ATTR SC,RW,MC
///GROUP1 RB1,BS1
///LVARS 5,0,0,0,0,11,11,0
NOP
'**************************
'**   Laser-Caliblation  **
'**      MACRO JOB       **
'**for Std. Laser-Tracker**
'**     DATE:2000,07,19  **
'**   UPDATE:2000,09,29  **
'**   UPDATE:2000,11,10  **
'**   UPDATE:2018,11,30  **
'**    ROBOT WITH BASE   **
'**************************
'=== initialize ===
'=== LT func. file# ===
GETARG LB000 IARG#(1)
' 
'=== LT calib. file# ===
GETARG LB001 IARG#(2)
' 
'=== LB# for rslt flag. ===
SET LB002 4
'=== set rslt flg 0:ng 1:ok ===
SET LB004 0
' 
GETS LPX010 $PX000
GETS LPX000 $PX000
CNVRT LPX000 LPX000 BF
SET LBP001 LBP000
SET LBP003 LBP000
SET LBP005 LBP000
SET LBP007 LBP000
SET LP001 LP000
SET LP002 LP000
SET LP003 LP000
SET LP004 LP000
SET LP005 LP000
SET LP006 LP000
SET LP007 LP000
SET LP008 LP000
'======================
' escape pos.
'======================
GETARG LP010 IARG#(8)
MOVJ LP010 LBP000 VJ=1.11
' 
'======================
' refp.[0]:tool pos.
'======================
GETARG LP000 IARG#(3)
CNVRT LPX000 LPX000 BF
MOVL LP000 LBP000 V=10.0 PL=0
' 
'escape pos.
MOVJ LP010 LBP000 VJ=0.67
' 
'=======================
' refp.[1]:sensing pos.
'=======================
SET LB003 2
GETARG LP001 IARG#(4)
CNVRT LPX001 LPX001 BF
SET LP002 LP001
SET LBP002 LBP001
MOVL LP001 LBP000 V=10.0 PL=0
SKILLSND "LGETPOS LT:0 B0:3 B1:2"
' 
'escape pos.
MOVJ LP010 LBP000 VJ=0.67
' 
'=======================
' refp.[2]:sensing pos.
'=======================
SET LB003 4
GETARG LP003 IARG#(5)
CNVRT LPX003 LPX003 BF
SET LP004 LP003
MOVL LP003 LBP000 V=10.0 PL=0
SKILLSND "LGETPOS LT:0 B0:3 B1:2"
' 
'escape pos.
MOVJ LP010 LBP000 VJ=0.67
' 
'=======================
' refp.[3]:sensing pos.
'=======================
SET LB003 6
GETARG LP005 IARG#(6)
CNVRT LPX005 LPX005 BF
SET LP006 LP005
MOVL LP005 LBP000 V=10.0 PL=0
SKILLSND "LGETPOS LT:0 B0:3 B1:2"
' 
'escape pos.
MOVJ LP010 LBP000 VJ=0.67
' 
'=======================
' refp.[4]:sensing pos.
'=======================
SET LB003 8
GETARG LP007 IARG#(7)
CNVRT LPX007 LPX007 BF
SET LP008 LP007
MOVL LP007 LBP000 V=10.0 PL=0
SKILLSND "LGETPOS LT:0 B0:3 B1:2"
' 
'escape pos.
MOVJ LP010 LBP000 VJ=0.67
' 
'======================
' exec. calibration
'======================
SET LB003 0
SKILLSND "LCLB LC:1 B0:3"
' 
'=== set rslt flg. ===
GETARG LB002 IARG#(9)
SET B[LB002] LB004
' 
END
