/JOB
//NAME LTSRCHS1
//POS
///NPOS 0,0,0,0,0,0
//INST
///DATE 2019/11/18 18:14
///ATTR SC,RW,MC
///GROUP1 RB1,BS1
///GROUP2 ST1
///LVARS 10,5,5,5,0,5,5,5
NOP
'**************************
'**   Laser-Search func. **
'**      MACRO JOB       **
'**   ROBOT WITH BASE    **
'**   [R1+B1+S1(2) Sync] **
'**for Std. Laser-Tracker**
'**     DATE:2000.11.13  **
'**   0...ERROR  1...OK  **
'**   UPDATE:2019,11,15  **
'**************************
'=== initialize ============
GETS LPX000 $PX000
'--- srch. rslt. -----------
GETARG LB005 IARG#(9)
SET B[LB005] 1
'--- LT mode ---------------
GETARG LB000 IARG#(1)
'--- LT mode by DIN#(*)----
GETARG LI000 IARG#(11)
' 
JUMP *skp-srch IF LB000=0
JUMP *skp-srch IF IN#(LI000)=ON
' 
'--- init SRCH -------------
SKILLSND "LSRCHOF"
'--- LT func. file# --------
GETARG LB001 IARG#(2)
' 
'--- srch-start refp. ------
GETARG LP001 IARG#(3)
GETARG LEX001 IARG#(4)
'--- srch-end refp. --------
GETARG LP002 IARG#(5)
GETARG LEX002 IARG#(6)
'--- speed-------------------
GETARG LI001 IARG#(7)
SET LI001  EXPRESS LI001 * 10 / 6
'--- detect-pos No.----------
GETARG LB006 IARG#(8)
'--- detect-pos -------------
SET LP003 LP000
SET LEX003 LEX000
SET LBP003 LBP000
SUB LP003 LP003
SUB LEX003 LEX003
SUB LBP003 LBP003
SET LP004 LP003
SET LEX004 LEX003
SET LBP004 LBP003
SET LB002 4
'--- LB# for rslt flg. ------
SET LB003 4
'--- set rslt. flg. 0:ng 1:ok ---
SET LB004 0
'--- gap mount --------------
GETARG LB007 IARG#(10)
SET LB008 0
' 
SMOVL LP001 LBP000 V=16.7 PL=0  +MOVJ LEX001
SKILLSND "LSRCHST LT:1 B0:2 B1:3 B2:8"
SMOVL LP002 LBP000 V=LI001 PL=0  +MOVJ LEX002
SKILLSND "LSRCHOF"
'--- reset rslt. ------------
SUB LP003 LP003
SUB LEX003 LEX003
SUB LBP003 LBP003
SET B[LB005] 0
SET P[LB006] LP003
SET EX[LB006] LEX003
SET BP[LB006] LBP003
SET R[LB007]  0.000000E+00
JUMP *skip IF LB004=0
'--- set rslt. --------------
SET B[LB005] LB004
SET P[LB006] LP004
SET EX[LB006] LEX004
SET BP[LB006] LBP004
SET R[LB007] LR000
*skip
*skp-srch
CLEAR LB005 1
END
